const std = @import("std");
const supervisor = @import("supervisor.zig");
const audio = @import("audio.zig");
const backoff = @import("backoff.zig");
const media = @import("media.zig");
const providers = @import("providers.zig");
const registry = @import("registry.zig");

const c = @cImport({
    @cDefine("WIN32_LEAN_AND_MEAN", "1");
    @cInclude("windows.h");
    @cInclude("psapi.h");
    @cInclude("tlhelp32.h");
});

const mutex_name = std.unicode.utf8ToUtf16LeStringLiteral("Local\\WeaverHostSingletonV2");
const shutdown_event_name = std.unicode.utf8ToUtf16LeStringLiteral("Local\\WeaverHostShutdownV2");
const reload_event_name = std.unicode.utf8ToUtf16LeStringLiteral("Local\\WeaverHostReloadV2");
const reload_signal_mutex_name = std.unicode.utf8ToUtf16LeStringLiteral("Local\\WeaverHostReloadSignalV2");
const reload_success_event_name = std.unicode.utf8ToUtf16LeStringLiteral("Local\\WeaverHostReloadSuccessV2");
const reload_failure_event_name = std.unicode.utf8ToUtf16LeStringLiteral("Local\\WeaverHostReloadFailureV2");
const env_pipe_name = std.unicode.utf8ToUtf16LeStringLiteral("WEAVER_HOST_PIPE");
const env_backend_file = std.unicode.utf8ToUtf16LeStringLiteral("WEAVER_BACKEND_FILE");
const env_renderer_pipe = std.unicode.utf8ToUtf16LeStringLiteral("WEAVER_RENDERER_PIPE");
const env_renderer_log = std.unicode.utf8ToUtf16LeStringLiteral("WEAVER_RENDERER_LOG");
const max_widgets = supervisor.max_widgets;
const max_path_bytes: usize = 2048;
const invalid_handle = c.INVALID_HANDLE_VALUE;

const WindowsSlotState = struct {
    process: c.HANDLE = null,
    pid: u32 = 0,
    pipe: c.HANDLE = invalid_handle,
    previous_process_ticks: u64 = 0,
    previous_sample_ms: u64 = 0,
    private_samples: [15]u64 = [_]u64{0} ** 15,
    cpu_samples: [15]f64 = [_]f64{0} ** 15,
    sample_count: usize = 0,
    sample_cursor: usize = 0,
    backend_path_buffer: [max_path_bytes]u8 = undefined,
    backend_path_len: usize = 0,

    fn backendPath(self: *const WindowsSlotState) []const u8 {
        return self.backend_path_buffer[0..self.backend_path_len];
    }

    fn averages(self: *const WindowsSlotState) struct { private_mb: f64, cpu: f64 } {
        if (self.sample_count == 0) return .{ .private_mb = 0, .cpu = 0 };
        var private_total: u64 = 0;
        var cpu_total: f64 = 0;
        for (self.private_samples[0..self.sample_count]) |value| private_total += value;
        for (self.cpu_samples[0..self.sample_count]) |value| cpu_total += value;
        return .{
            .private_mb = @as(f64, @floatFromInt(private_total)) / @as(f64, @floatFromInt(self.sample_count)) / (1024.0 * 1024.0),
            .cpu = cpu_total / @as(f64, @floatFromInt(self.sample_count)),
        };
    }
};

const Slot = supervisor.Slot(WindowsSlotState);

const WindowsRendererState = struct {
    process: c.HANDLE = null,
    pid: u32 = 0,
    previous_process_ticks: u64 = 0,
    previous_sample_ms: u64 = 0,
    private_samples: [15]u64 = [_]u64{0} ** 15,
    cpu_samples: [15]f64 = [_]f64{0} ** 15,
    sample_count: usize = 0,
    sample_cursor: usize = 0,

    fn averages(self: *const WindowsRendererState) struct { private_mb: f64, cpu: f64 } {
        if (self.sample_count == 0) return .{ .private_mb = 0, .cpu = 0 };
        var private_total: u64 = 0;
        var cpu_total: f64 = 0;
        for (self.private_samples[0..self.sample_count]) |value| private_total += value;
        for (self.cpu_samples[0..self.sample_count]) |value| cpu_total += value;
        return .{
            .private_mb = @as(f64, @floatFromInt(private_total)) / @as(f64, @floatFromInt(self.sample_count)) / (1024.0 * 1024.0),
            .cpu = cpu_total / @as(f64, @floatFromInt(self.sample_count)),
        };
    }
};

const RendererState = supervisor.RendererState(WindowsRendererState);

const Host = struct {
    io: std.Io,
    allocator: std.mem.Allocator,
    repo_root: []const u8,
    registry_path: []const u8,
    status_path: []const u8,
    status_temp_path: []const u8,
    runtime_exe: []const u8,
    renderer_exe: []const u8,
    renderer_pipe_name: []const u8,
    renderer_log_path: []const u8,
    cli_script: []const u8,
    force_software: bool,
    renderer: RendererState = .{},
    slots: [max_widgets]Slot = [_]Slot{.{}} ** max_widgets,
    sampler: providers.Sampler = .{},
    audio_provider: audio.Provider = .{},
    media_provider: media.Provider = .{},
    previous_cpu: [8192]u8 = undefined,
    previous_cpu_len: usize = 0,
    previous_memory: [512]u8 = undefined,
    previous_memory_len: usize = 0,
    previous_media: [2048]u8 = undefined,
    previous_media_len: usize = 0,
    audio_pipe_frames: u64 = 0,
    media_pipe_frames: u64 = 0,
    thread_sample_ms: u64 = 0,
    slot_threads: [max_widgets]u32 = [_]u32{0} ** max_widgets,
    renderer_threads: u32 = 0,

    fn loadRegistry(self: *Host) !void {
        const owned_bytes = std.Io.Dir.cwd().readFileAlloc(self.io, self.registry_path, self.allocator, .limited(256 * 1024)) catch |err| switch (err) {
            error.FileNotFound => null,
            else => return err,
        };
        defer if (owned_bytes) |bytes| self.allocator.free(bytes);
        const bytes = owned_bytes orelse "{\"widgets\":[]}";
        const parsed = try registry.parse(self.allocator, bytes);
        defer parsed.deinit();
        try supervisor.reconcile(&self.slots, parsed.value.widgets, self);
    }

    pub fn running(self: *const Host, index: usize) bool {
        return self.slots[index].platform.process != null;
    }

    pub fn stop(self: *Host, index: usize, graceful: bool) void {
        self.stopSlot(&self.slots[index], graceful);
    }

    fn supervise(self: *Host, now_ms: u64) void {
        self.superviseRenderer(now_ms);
        for (&self.slots) |*slot| {
            if (!slot.used or !slot.enabled) continue;
            const process_running = slot.platform.process != null;
            const process_exited = if (slot.platform.process) |process| c.WaitForSingleObject(process, 0) == c.WAIT_OBJECT_0 else false;
            switch (supervisor.nextSlotAction(slot, self.sourceExists(slot.source()), process_running, process_exited, now_ms)) {
                .none => {},
                .stop_missing => self.stopSlot(slot, true),
                .handle_exit => self.handleExit(slot, now_ms),
                .launch => self.launch(slot, now_ms) catch |err| {
                    slot.setReason("launch failed: {s}", .{@errorName(err)});
                    supervisor.recordCrash(slot, now_ms, null);
                },
            }
        }
        self.superviseRenderer(now_ms);
    }

    fn launch(self: *Host, slot: *Slot, now_ms: u64) !void {
        try self.ensureBundle(slot.source());
        const dist = try std.fs.path.join(self.allocator, &.{ slot.source(), "dist" });
        defer self.allocator.free(dist);
        const manifest_path = try std.fs.path.join(self.allocator, &.{ dist, "widget.json" });
        defer self.allocator.free(manifest_path);
        const manifest_bytes = try std.Io.Dir.cwd().readFileAlloc(self.io, manifest_path, self.allocator, .limited(64 * 1024));
        defer self.allocator.free(manifest_bytes);
        const Manifest = struct {
            subscribe: []const []const u8 = &.{},
            renderBackend: []const u8 = "software",
        };
        const manifest = try std.json.parseFromSlice(Manifest, self.allocator, manifest_bytes, .{ .ignore_unknown_fields = true });
        defer manifest.deinit();
        supervisor.selectManifest(slot, manifest.value.subscribe, manifest.value.renderBackend, self.force_software);
        if (slot.wants_gpu) self.ensureRenderer(now_ms) catch {};
        var pipe_name_buffer: [256]u8 = undefined;
        var pipe_name: []const u8 = &.{};
        if (slot.wants_cpu or slot.wants_memory or slot.wants_audio or slot.wants_media) {
            pipe_name = try std.fmt.bufPrint(&pipe_name_buffer, "\\\\.\\pipe\\weaver-{d}-{x}", .{ c.GetCurrentProcessId(), std.hash.Wyhash.hash(now_ms, slot.name()) });
            const pipe_name_w = try std.unicode.utf8ToUtf16LeAllocZ(self.allocator, pipe_name);
            defer self.allocator.free(pipe_name_w);
            slot.platform.pipe = c.CreateNamedPipeW(pipe_name_w.ptr, c.PIPE_ACCESS_OUTBOUND, c.PIPE_TYPE_BYTE | c.PIPE_READMODE_BYTE | c.PIPE_NOWAIT, 1, 8192, 8192, 0, null);
            if (slot.platform.pipe == invalid_handle) return error.CreatePipeFailed;
        }
        errdefer self.closePipe(slot);
        const command = if (slot.dev)
            try std.fmt.allocPrint(self.allocator, "\"{s}\" --dev \"{s}\"", .{ self.runtime_exe, dist })
        else
            try std.fmt.allocPrint(self.allocator, "\"{s}\" \"{s}\"", .{ self.runtime_exe, dist });
        defer self.allocator.free(command);
        const command_w_const = try std.unicode.utf8ToUtf16LeAllocZ(self.allocator, command);
        defer self.allocator.free(command_w_const);
        const command_w = try self.allocator.dupeZ(u16, command_w_const);
        defer self.allocator.free(command_w);
        if (pipe_name.len > 0) {
            const value_w = try std.unicode.utf8ToUtf16LeAllocZ(self.allocator, pipe_name);
            defer self.allocator.free(value_w);
            if (c.SetEnvironmentVariableW(env_pipe_name, value_w.ptr) == 0) return error.SetEnvironmentFailed;
        }
        if (slot.wants_gpu) {
            const renderer_pipe_w = try std.unicode.utf8ToUtf16LeAllocZ(self.allocator, self.renderer_pipe_name);
            defer self.allocator.free(renderer_pipe_w);
            if (c.SetEnvironmentVariableW(env_renderer_pipe, renderer_pipe_w.ptr) == 0) return error.SetEnvironmentFailed;
        }
        const backend_hash = std.hash.Wyhash.hash(0, slot.name());
        const backend_path = try std.fmt.bufPrint(&slot.platform.backend_path_buffer, "{s}.backend-{x}", .{ self.status_path, backend_hash });
        slot.platform.backend_path_len = backend_path.len;
        std.Io.Dir.cwd().deleteFile(self.io, backend_path) catch {};
        const backend_path_w = try std.unicode.utf8ToUtf16LeAllocZ(self.allocator, backend_path);
        defer self.allocator.free(backend_path_w);
        if (c.SetEnvironmentVariableW(env_backend_file, backend_path_w.ptr) == 0) return error.SetEnvironmentFailed;
        defer {
            if (pipe_name.len > 0) _ = c.SetEnvironmentVariableW(env_pipe_name, null);
            if (slot.wants_gpu) _ = c.SetEnvironmentVariableW(env_renderer_pipe, null);
            _ = c.SetEnvironmentVariableW(env_backend_file, null);
        }
        var startup: c.STARTUPINFOW = std.mem.zeroes(c.STARTUPINFOW);
        startup.cb = @sizeOf(c.STARTUPINFOW);
        var process_info: c.PROCESS_INFORMATION = std.mem.zeroes(c.PROCESS_INFORMATION);
        if (c.CreateProcessW(null, command_w.ptr, null, null, 0, c.CREATE_NO_WINDOW, null, null, &startup, &process_info) == 0) return error.CreateProcessFailed;
        _ = c.CloseHandle(process_info.hThread);
        slot.platform.process = process_info.hProcess;
        slot.platform.pid = process_info.dwProcessId;
        self.thread_sample_ms = 0;
        if (slot.platform.pipe != invalid_handle) {
            if (!connectPipe(slot.platform.pipe, slot.platform.process, 5_000)) {
                _ = c.TerminateProcess(slot.platform.process, 1);
                return error.ConnectPipeFailed;
            }
        }
        supervisor.markRunning(slot, now_ms, self.artifactMtime(slot.source()));
        slot.platform.previous_process_ticks = processTicks(slot.platform.process);
        slot.platform.previous_sample_ms = now_ms;
        slot.platform.sample_count = 0;
        slot.platform.sample_cursor = 0;
    }

    fn rendererDesired(self: *const Host) bool {
        return supervisor.rendererDesired(&self.slots, self.force_software);
    }

    fn superviseRenderer(self: *Host, now_ms: u64) void {
        const renderer_running = self.renderer.platform.process != null;
        const exited = if (self.renderer.platform.process) |process| c.WaitForSingleObject(process, 0) == c.WAIT_OBJECT_0 else false;
        switch (supervisor.nextRendererAction(renderer_running, exited, self.rendererDesired(), now_ms, self.renderer.next_restart_ms)) {
            .none => {},
            .handle_exit => {
                const process = self.renderer.platform.process.?;
                _ = c.CloseHandle(process);
                self.renderer.platform.process = null;
                self.renderer.platform.pid = 0;
                self.thread_sample_ms = 0;
                supervisor.recordRendererExit(&self.renderer, now_ms);
            },
            .stop => {
                const process = self.renderer.platform.process.?;
                _ = c.TerminateProcess(process, 0);
                _ = c.WaitForSingleObject(process, 1000);
                _ = c.CloseHandle(process);
                self.renderer = .{};
                self.thread_sample_ms = 0;
            },
            .launch => self.ensureRenderer(now_ms) catch {
                self.renderer.next_restart_ms = now_ms + 1000;
            },
        }
    }

    fn ensureRenderer(self: *Host, now_ms: u64) !void {
        if (self.force_software or self.renderer.platform.process != null or now_ms < self.renderer.next_restart_ms) return;
        const command = try std.fmt.allocPrint(self.allocator, "\"{s}\"", .{self.renderer_exe});
        defer self.allocator.free(command);
        const command_w_const = try std.unicode.utf8ToUtf16LeAllocZ(self.allocator, command);
        defer self.allocator.free(command_w_const);
        const command_w = try self.allocator.dupeZ(u16, command_w_const);
        defer self.allocator.free(command_w);
        const pipe_w = try std.unicode.utf8ToUtf16LeAllocZ(self.allocator, self.renderer_pipe_name);
        defer self.allocator.free(pipe_w);
        const log_w = try std.unicode.utf8ToUtf16LeAllocZ(self.allocator, self.renderer_log_path);
        defer self.allocator.free(log_w);
        if (c.SetEnvironmentVariableW(env_renderer_pipe, pipe_w.ptr) == 0) return error.SetEnvironmentFailed;
        if (c.SetEnvironmentVariableW(env_renderer_log, log_w.ptr) == 0) return error.SetEnvironmentFailed;
        defer {
            _ = c.SetEnvironmentVariableW(env_renderer_pipe, null);
            _ = c.SetEnvironmentVariableW(env_renderer_log, null);
        }
        var startup: c.STARTUPINFOW = std.mem.zeroes(c.STARTUPINFOW);
        startup.cb = @sizeOf(c.STARTUPINFOW);
        var info: c.PROCESS_INFORMATION = std.mem.zeroes(c.PROCESS_INFORMATION);
        if (c.CreateProcessW(null, command_w.ptr, null, null, 0, c.CREATE_NO_WINDOW, null, null, &startup, &info) == 0) return error.CreateRendererFailed;
        _ = c.CloseHandle(info.hThread);
        self.renderer.platform.process = info.hProcess;
        self.renderer.platform.pid = info.dwProcessId;
        self.thread_sample_ms = 0;
        self.renderer.started_ms = now_ms;
        self.renderer.platform.previous_process_ticks = processTicks(info.hProcess);
        self.renderer.platform.previous_sample_ms = now_ms;
        self.renderer.platform.sample_count = 0;
        self.renderer.platform.sample_cursor = 0;
    }

    fn handleExit(self: *Host, slot: *Slot, now_ms: u64) void {
        var exit_code: c.DWORD = 1;
        _ = c.GetExitCodeProcess(slot.platform.process, &exit_code);
        self.closeProcess(slot);
        if (!self.sourceExists(slot.source())) {
            _ = supervisor.nextSlotAction(slot, false, false, false, now_ms);
            return;
        }
        supervisor.recordCrash(slot, now_ms, exit_code);
    }

    fn stopSlot(self: *Host, slot: *Slot, graceful: bool) void {
        if (slot.platform.process) |process| {
            if (graceful) {
                closeWindowsForProcess(slot.platform.pid);
                if (c.WaitForSingleObject(process, 1500) != c.WAIT_OBJECT_0) _ = c.TerminateProcess(process, 0);
            } else _ = c.TerminateProcess(process, 0);
            _ = c.WaitForSingleObject(process, 1000);
        }
        self.closeProcess(slot);
    }

    fn closeProcess(self: *Host, slot: *Slot) void {
        self.closePipe(slot);
        if (slot.platform.process) |process| _ = c.CloseHandle(process);
        slot.platform.process = null;
        slot.platform.pid = 0;
        self.thread_sample_ms = 0;
        slot.platform.previous_process_ticks = 0;
    }

    fn closePipe(_: *Host, slot: *Slot) void {
        if (slot.platform.pipe == invalid_handle) return;
        _ = c.FlushFileBuffers(slot.platform.pipe);
        _ = c.DisconnectNamedPipe(slot.platform.pipe);
        _ = c.CloseHandle(slot.platform.pipe);
        slot.platform.pipe = invalid_handle;
    }

    fn sampleCosts(self: *Host, now_ms: u64) void {
        for (&self.slots) |*slot| {
            const process = slot.platform.process orelse continue;
            var counters: c.PROCESS_MEMORY_COUNTERS_EX = std.mem.zeroes(c.PROCESS_MEMORY_COUNTERS_EX);
            counters.cb = @sizeOf(c.PROCESS_MEMORY_COUNTERS_EX);
            if (c.GetProcessMemoryInfo(process, @ptrCast(&counters), @sizeOf(c.PROCESS_MEMORY_COUNTERS_EX)) == 0) continue;
            const ticks = processTicks(process);
            const elapsed_ms = now_ms -| slot.platform.previous_sample_ms;
            const tick_delta = ticks -| slot.platform.previous_process_ticks;
            const cpu = if (elapsed_ms == 0) 0 else 100.0 * @as(f64, @floatFromInt(tick_delta)) / 10_000.0 / @as(f64, @floatFromInt(elapsed_ms));
            slot.platform.private_samples[slot.platform.sample_cursor] = counters.PrivateUsage;
            slot.platform.cpu_samples[slot.platform.sample_cursor] = @round(cpu * 10.0) / 10.0;
            slot.platform.sample_cursor = (slot.platform.sample_cursor + 1) % slot.platform.private_samples.len;
            slot.platform.sample_count = @min(slot.platform.sample_count + 1, slot.platform.private_samples.len);
            slot.platform.previous_process_ticks = ticks;
            slot.platform.previous_sample_ms = now_ms;
        }
        if (self.renderer.platform.process) |process| {
            var counters: c.PROCESS_MEMORY_COUNTERS_EX = std.mem.zeroes(c.PROCESS_MEMORY_COUNTERS_EX);
            counters.cb = @sizeOf(c.PROCESS_MEMORY_COUNTERS_EX);
            if (c.GetProcessMemoryInfo(process, @ptrCast(&counters), @sizeOf(c.PROCESS_MEMORY_COUNTERS_EX)) != 0) {
                const ticks = processTicks(process);
                const elapsed_ms = now_ms -| self.renderer.platform.previous_sample_ms;
                const tick_delta = ticks -| self.renderer.platform.previous_process_ticks;
                const cpu = if (elapsed_ms == 0) 0 else 100.0 * @as(f64, @floatFromInt(tick_delta)) / 10_000.0 / @as(f64, @floatFromInt(elapsed_ms));
                self.renderer.platform.private_samples[self.renderer.platform.sample_cursor] = counters.PrivateUsage;
                self.renderer.platform.cpu_samples[self.renderer.platform.sample_cursor] = @round(cpu * 10.0) / 10.0;
                self.renderer.platform.sample_cursor = (self.renderer.platform.sample_cursor + 1) % self.renderer.platform.private_samples.len;
                self.renderer.platform.sample_count = @min(self.renderer.platform.sample_count + 1, self.renderer.platform.private_samples.len);
                self.renderer.platform.previous_process_ticks = ticks;
                self.renderer.platform.previous_sample_ms = now_ms;
            }
        }
    }

    fn sampleProviders(self: *Host) void {
        var any = false;
        for (&self.slots) |*slot| if (slot.platform.process != null and (slot.wants_cpu or slot.wants_memory)) {
            any = true;
            break;
        };
        if (!any) return;
        const sample = self.sampler.sample() catch return orelse return;
        var cpu_buffer: [8192]u8 = undefined;
        const cpu = providers.formatCpu(sample.cpu, &cpu_buffer) catch return;
        var memory_buffer: [512]u8 = undefined;
        const memory = providers.formatMemory(sample.memory, &memory_buffer) catch return;
        const cpu_changed = !std.mem.eql(u8, cpu, self.previous_cpu[0..self.previous_cpu_len]);
        const memory_changed = !std.mem.eql(u8, memory, self.previous_memory[0..self.previous_memory_len]);
        for (&self.slots) |*slot| {
            if (slot.platform.pipe == invalid_handle) continue;
            if (slot.wants_cpu and (!slot.cpu_sent or cpu_changed) and writePipe(slot.platform.pipe, cpu)) slot.cpu_sent = true;
            if (slot.wants_memory and (!slot.memory_sent or memory_changed) and writePipe(slot.platform.pipe, memory)) slot.memory_sent = true;
        }
        @memcpy(self.previous_cpu[0..cpu.len], cpu);
        self.previous_cpu_len = cpu.len;
        @memcpy(self.previous_memory[0..memory.len], memory);
        self.previous_memory_len = memory.len;
    }

    fn hasAudioSubscribers(self: *const Host) bool {
        return supervisor.hasSubscription(&self.slots, .audio, self);
    }

    fn hasMediaSubscribers(self: *const Host) bool {
        return supervisor.hasSubscription(&self.slots, .media, self);
    }

    fn sampleAudio(self: *Host, now_ms: u64) void {
        const active = self.hasAudioSubscribers();
        self.audio_provider.setActive(active, now_ms);
        if (!active) return;
        const frame = self.audio_provider.poll(now_ms) orelse return;
        var buffer: [512]u8 = undefined;
        const encoded = audio.formatFrame(frame, &buffer) catch return;
        var delivered = false;
        for (&self.slots) |*slot| {
            if (slot.platform.pipe == invalid_handle or !slot.wants_audio) continue;
            if (writePipe(slot.platform.pipe, encoded)) delivered = true;
        }
        if (delivered) self.audio_pipe_frames += 1;
    }

    fn sampleMedia(self: *Host, now_ms: u64) void {
        const active = self.hasMediaSubscribers();
        self.media_provider.setActive(active, now_ms);
        if (!active) return;
        const frame = self.media_provider.poll(now_ms) orelse return;
        var buffer: [2048]u8 = undefined;
        const encoded = media.formatFrame(&frame, &buffer) catch return;
        const changed = !std.mem.eql(u8, encoded, self.previous_media[0..self.previous_media_len]);
        var delivered = false;
        for (&self.slots) |*slot| {
            if (slot.platform.pipe == invalid_handle or !slot.wants_media) continue;
            if ((!slot.media_sent or changed) and writePipe(slot.platform.pipe, encoded)) {
                slot.media_sent = true;
                delivered = true;
            }
        }
        if (delivered) self.media_pipe_frames += 1;
        @memcpy(self.previous_media[0..encoded.len], encoded);
        self.previous_media_len = encoded.len;
    }

    fn writeStatus(self: *Host, now_ms: u64) void {
        if (self.thread_sample_ms == 0 or now_ms -| self.thread_sample_ms >= 30_000) {
            self.slot_threads = [_]u32{0} ** max_widgets;
            self.renderer_threads = 0;
            collectThreadCounts(self, &self.slot_threads, &self.renderer_threads);
            self.thread_sample_ms = now_ms;
        }
        var entries: [max_widgets + 1]supervisor.StatusEntry = undefined;
        var entry_count: usize = 0;
        var backend_allocations = [_]?[]u8{null} ** max_widgets;
        defer for (backend_allocations) |value| if (value) |bytes| self.allocator.free(bytes);
        if (self.renderer.platform.process != null) {
            const average = self.renderer.platform.averages();
            entries[entry_count] = .{
                .name = "renderer",
                .pid = self.renderer.platform.pid,
                .private_mb = average.private_mb,
                .cpu_percent = average.cpu,
                .threads = self.renderer_threads,
                .backend = "gpu",
                .uptime_seconds = (now_ms -| self.renderer.started_ms) / 1000,
                .state = .running,
                .reason = "",
            };
            entry_count += 1;
        }
        for (&self.slots, 0..) |*slot, slot_index| {
            if (!slot.used) continue;
            const average = slot.platform.averages();
            var backend: []const u8 = "-";
            if (slot.platform.process != null and slot.platform.backend_path_len > 0) {
                backend_allocations[slot_index] = std.Io.Dir.cwd().readFileAlloc(self.io, slot.platform.backendPath(), self.allocator, .limited(16)) catch null;
                if (backend_allocations[slot_index]) |value| backend = value;
            }
            entries[entry_count] = .{
                .name = slot.name(),
                .pid = slot.platform.pid,
                .private_mb = average.private_mb,
                .cpu_percent = average.cpu,
                .threads = self.slot_threads[slot_index],
                .backend = backend,
                .uptime_seconds = if (slot.platform.process != null) (now_ms -| slot.started_ms) / 1000 else 0,
                .state = slot.state,
                .reason = slot.reason(),
            };
            entry_count += 1;
        }
        var output: std.Io.Writer.Allocating = .init(self.allocator);
        defer output.deinit();
        supervisor.writeStatus(&output.writer, c.GetCurrentProcessId(), .{
            .audio_capture_active = self.audio_provider.capture != null,
            .audio_silent = self.audio_provider.silent,
            .audio_pipe_frames = self.audio_pipe_frames,
            .media_pipe_frames = self.media_pipe_frames,
        }, entries[0..entry_count]) catch return;
        var cwd = std.Io.Dir.cwd();
        cwd.writeFile(self.io, .{ .sub_path = self.status_temp_path, .data = output.written() }) catch return;
        cwd.rename(self.status_temp_path, cwd, self.status_path, self.io) catch return;
    }

    fn ensureBundle(self: *Host, source: []const u8) !void {
        const source_path = try std.fs.path.join(self.allocator, &.{ source, "widget.tsx" });
        defer self.allocator.free(source_path);
        const bundle_path = try std.fs.path.join(self.allocator, &.{ source, "dist", "bundle.js" });
        defer self.allocator.free(bundle_path);
        const manifest_path = try std.fs.path.join(self.allocator, &.{ source, "dist", "widget.json" });
        defer self.allocator.free(manifest_path);
        const source_stat = try std.Io.Dir.cwd().statFile(self.io, source_path, .{});
        const bundle_stat = std.Io.Dir.cwd().statFile(self.io, bundle_path, .{}) catch null;
        const manifest_stat = std.Io.Dir.cwd().statFile(self.io, manifest_path, .{}) catch null;
        if (bundle_stat != null and manifest_stat != null and bundle_stat.?.mtime.nanoseconds >= source_stat.mtime.nanoseconds) return;
        const result = try std.process.run(self.allocator, self.io, .{
            .argv = &.{ "node", self.cli_script, "bundle", source },
            .stdout_limit = .limited(64 * 1024),
            .stderr_limit = .limited(64 * 1024),
            .create_no_window = true,
        });
        defer self.allocator.free(result.stdout);
        defer self.allocator.free(result.stderr);
        switch (result.term) {
            .exited => |code| if (code != 0) return error.BundleFailed,
            else => return error.BundleFailed,
        }
    }

    pub fn artifactMtime(self: *Host, source: []const u8) i128 {
        const path = std.fs.path.join(self.allocator, &.{ source, "dist", "bundle.js" }) catch return 0;
        defer self.allocator.free(path);
        const stat = std.Io.Dir.cwd().statFile(self.io, path, .{}) catch return 0;
        return stat.mtime.nanoseconds;
    }

    fn sourceExists(self: *Host, source: []const u8) bool {
        _ = std.Io.Dir.cwd().statFile(self.io, source, .{}) catch return false;
        return true;
    }
};

pub fn main(init: std.process.Init) void {
    run(init) catch |err| {
        std.debug.print("error: {s}\n", .{@errorName(err)});
        std.process.exit(1);
    };
}

fn run(init: std.process.Init) !void {
    const allocator = std.heap.page_allocator;
    const args = try init.minimal.args.toSlice(init.arena.allocator());
    if (args.len == 2 and std.mem.eql(u8, args[1], "--signal-down")) return signalEvent(shutdown_event_name);
    if (args.len == 2 and std.mem.eql(u8, args[1], "--signal-reload")) return signalReloadAndWait();
    if (args.len == 2 and std.mem.eql(u8, args[1], "--probe")) {
        const handle = c.OpenMutexW(c.SYNCHRONIZE, 0, mutex_name);
        if (handle == null) return error.HostNotRunning;
        _ = c.CloseHandle(handle);
        return;
    }
    if (args.len == 2 and std.mem.eql(u8, args[1], "--probe-reload-ready")) {
        const handle = c.OpenMutexW(c.SYNCHRONIZE, 0, mutex_name);
        if (handle == null) return error.HostNotRunning;
        _ = c.CloseHandle(handle);
        const ready = c.OpenEventW(c.SYNCHRONIZE, 0, reload_failure_event_name);
        if (ready == null) return error.HostNotRunning;
        _ = c.CloseHandle(ready);
        return;
    }
    const mutex = c.CreateMutexW(null, 0, mutex_name) orelse return error.CreateMutexFailed;
    defer _ = c.CloseHandle(mutex);
    if (c.GetLastError() == c.ERROR_ALREADY_EXISTS) return;
    const shutdown_event = c.CreateEventW(null, 1, 0, shutdown_event_name) orelse return error.CreateEventFailed;
    defer _ = c.CloseHandle(shutdown_event);
    const reload_event = c.CreateEventW(null, 0, 0, reload_event_name) orelse return error.CreateEventFailed;
    defer _ = c.CloseHandle(reload_event);
    const reload_success_event = c.CreateEventW(null, 1, 0, reload_success_event_name) orelse return error.CreateEventFailed;
    defer _ = c.CloseHandle(reload_success_event);
    const reload_failure_event = c.CreateEventW(null, 1, 0, reload_failure_event_name) orelse return error.CreateEventFailed;
    defer _ = c.CloseHandle(reload_failure_event);
    const local_app_data = init.environ_map.get("LOCALAPPDATA") orelse return error.MissingLocalAppData;
    const directory = try std.fs.path.join(allocator, &.{ local_app_data, "weaver" });
    defer allocator.free(directory);
    try std.Io.Dir.cwd().createDirPath(init.io, directory);
    const registry_path = try std.fs.path.join(allocator, &.{ directory, "registry.json" });
    defer allocator.free(registry_path);
    const status_path = try std.fs.path.join(allocator, &.{ directory, "status.json" });
    defer allocator.free(status_path);
    const status_temp_path = try std.fmt.allocPrint(allocator, "{s}.tmp", .{status_path});
    defer allocator.free(status_temp_path);
    const repo_root = try repositoryRoot(allocator);
    defer allocator.free(repo_root);
    const runtime_exe = try std.fs.path.join(allocator, &.{ repo_root, "runtime", "zig-out", "bin", "weaver-widget.exe" });
    defer allocator.free(runtime_exe);
    const renderer_exe = try std.fs.path.join(allocator, &.{ repo_root, "renderer", "zig-out", "bin", "weaver-renderer.exe" });
    defer allocator.free(renderer_exe);
    const renderer_pipe_name = try std.fmt.allocPrint(allocator, "\\\\.\\pipe\\weaver-renderer-{d}", .{c.GetCurrentProcessId()});
    defer allocator.free(renderer_pipe_name);
    const renderer_log_path = try std.fmt.allocPrint(allocator, "{s}.renderer.log", .{status_path});
    defer allocator.free(renderer_log_path);
    std.Io.Dir.cwd().deleteFile(init.io, renderer_log_path) catch {};
    const cli_script = try std.fs.path.join(allocator, &.{ repo_root, "cli", "dist", "index.js" });
    defer allocator.free(cli_script);
    var host: Host = .{
        .io = init.io,
        .allocator = allocator,
        .repo_root = repo_root,
        .registry_path = registry_path,
        .status_path = status_path,
        .status_temp_path = status_temp_path,
        .runtime_exe = runtime_exe,
        .renderer_exe = renderer_exe,
        .renderer_pipe_name = renderer_pipe_name,
        .renderer_log_path = renderer_log_path,
        .cli_script = cli_script,
        .force_software = if (init.environ_map.get("WEAVER_FORCE_SOFTWARE")) |value| std.mem.eql(u8, value, "1") else false,
    };
    defer host.audio_provider.deinit();
    defer host.media_provider.deinit();
    try host.loadRegistry();
    var next_provider_ms: u64 = 0;
    var next_cost_ms: u64 = 0;
    while (true) {
        const handles = [_]c.HANDLE{ shutdown_event, reload_event };
        const wait_ms: c.DWORD = if (host.hasAudioSubscribers()) 10 else 250;
        const wait = c.WaitForMultipleObjects(handles.len, &handles, 0, wait_ms);
        if (wait == c.WAIT_OBJECT_0) break;
        if (wait == c.WAIT_OBJECT_0 + 1) {
            host.loadRegistry() catch {
                _ = c.SetEvent(reload_failure_event);
                continue;
            };
            _ = c.SetEvent(reload_success_event);
        }
        const now = c.GetTickCount64();
        host.supervise(now);
        host.sampleAudio(now);
        host.sampleMedia(now);
        if (now >= next_provider_ms) {
            host.sampleProviders();
            next_provider_ms = now + 1000;
        }
        if (now >= next_cost_ms) {
            host.sampleCosts(now);
            host.writeStatus(now);
            next_cost_ms = now + 2000;
        }
    }
    for (&host.slots) |*slot| if (slot.platform.process != null) host.stopSlot(slot, true);
    if (host.renderer.platform.process) |process| {
        _ = c.TerminateProcess(process, 0);
        _ = c.WaitForSingleObject(process, 1000);
        _ = c.CloseHandle(process);
        host.renderer.platform.process = null;
    }
    host.writeStatus(c.GetTickCount64());
}

fn signalEvent(name: [*:0]const u16) !void {
    const event = c.OpenEventW(c.EVENT_MODIFY_STATE, 0, name) orelse return error.HostNotRunning;
    defer _ = c.CloseHandle(event);
    if (c.SetEvent(event) == 0) return error.SignalFailed;
}

fn signalReloadAndWait() !void {
    const signal_mutex = c.CreateMutexW(null, 0, reload_signal_mutex_name) orelse return error.CreateMutexFailed;
    defer _ = c.CloseHandle(signal_mutex);
    const mutex_wait = c.WaitForSingleObject(signal_mutex, 10_000);
    if (mutex_wait != c.WAIT_OBJECT_0 and mutex_wait != c.WAIT_ABANDONED) return error.ReloadSignalBusy;
    defer _ = c.ReleaseMutex(signal_mutex);

    const desired_access = c.SYNCHRONIZE | c.EVENT_MODIFY_STATE;
    const success_event = c.OpenEventW(desired_access, 0, reload_success_event_name) orelse return error.HostNotRunning;
    defer _ = c.CloseHandle(success_event);
    const failure_event = c.OpenEventW(desired_access, 0, reload_failure_event_name) orelse return error.HostNotRunning;
    defer _ = c.CloseHandle(failure_event);
    const reload_event = c.OpenEventW(c.EVENT_MODIFY_STATE, 0, reload_event_name) orelse return error.HostNotRunning;
    defer _ = c.CloseHandle(reload_event);
    if (c.ResetEvent(success_event) == 0 or c.ResetEvent(failure_event) == 0) return error.SignalFailed;
    if (c.SetEvent(reload_event) == 0) return error.SignalFailed;
    const acknowledgements = [_]c.HANDLE{ success_event, failure_event };
    const wait = c.WaitForMultipleObjects(acknowledgements.len, &acknowledgements, 0, 10_000);
    if (wait == c.WAIT_OBJECT_0) return;
    if (wait == c.WAIT_OBJECT_0 + 1) return error.RegistryReloadFailed;
    return error.RegistryReloadTimedOut;
}

fn repositoryRoot(allocator: std.mem.Allocator) ![]u8 {
    var path_w: [32768]u16 = undefined;
    const length = c.GetModuleFileNameW(null, &path_w, path_w.len);
    if (length == 0 or length == path_w.len) return error.ExecutablePathFailed;
    const path = try std.unicode.utf16LeToUtf8Alloc(allocator, path_w[0..length]);
    defer allocator.free(path);
    const bin_dir = std.fs.path.dirname(path) orelse return error.ExecutablePathFailed;
    const zig_out = std.fs.path.dirname(bin_dir) orelse return error.ExecutablePathFailed;
    const host_dir = std.fs.path.dirname(zig_out) orelse return error.ExecutablePathFailed;
    const repo = std.fs.path.dirname(host_dir) orelse return error.ExecutablePathFailed;
    return allocator.dupe(u8, repo);
}

fn processTicks(process: c.HANDLE) u64 {
    var created: c.FILETIME = undefined;
    var exited: c.FILETIME = undefined;
    var kernel: c.FILETIME = undefined;
    var user: c.FILETIME = undefined;
    if (c.GetProcessTimes(process, &created, &exited, &kernel, &user) == 0) return 0;
    return fileTime(kernel) + fileTime(user);
}

fn fileTime(value: c.FILETIME) u64 {
    return (@as(u64, value.dwHighDateTime) << 32) | value.dwLowDateTime;
}

fn collectThreadCounts(host: *const Host, slot_counts: *[max_widgets]u32, renderer_count: *u32) void {
    const snapshot = c.CreateToolhelp32Snapshot(c.TH32CS_SNAPTHREAD, 0);
    if (snapshot == invalid_handle) return;
    defer _ = c.CloseHandle(snapshot);
    var entry: c.THREADENTRY32 = std.mem.zeroes(c.THREADENTRY32);
    entry.dwSize = @sizeOf(c.THREADENTRY32);
    if (c.Thread32First(snapshot, &entry) == 0) return;
    while (true) {
        if (entry.th32OwnerProcessID == host.renderer.platform.pid) renderer_count.* += 1;
        for (&host.slots, 0..) |slot, index| {
            if (slot.platform.pid != 0 and entry.th32OwnerProcessID == slot.platform.pid) slot_counts[index] += 1;
        }
        if (c.Thread32Next(snapshot, &entry) == 0) break;
    }
}

fn writePipe(pipe: c.HANDLE, bytes: []const u8) bool {
    var written: c.DWORD = 0;
    return c.WriteFile(pipe, bytes.ptr, @intCast(bytes.len), &written, null) != 0 and written == bytes.len;
}

fn connectPipe(pipe: c.HANDLE, process: c.HANDLE, timeout_ms: u64) bool {
    const deadline = c.GetTickCount64() + timeout_ms;
    while (true) {
        if (c.ConnectNamedPipe(pipe, null) != 0 or c.GetLastError() == c.ERROR_PIPE_CONNECTED) break;
        const pipe_error = c.GetLastError();
        if (pipe_error != c.ERROR_PIPE_LISTENING and pipe_error != c.ERROR_NO_DATA) return false;
        if (c.WaitForSingleObject(process, 0) == c.WAIT_OBJECT_0 or c.GetTickCount64() >= deadline) return false;
        c.Sleep(10);
    }
    var mode: c.DWORD = c.PIPE_READMODE_BYTE | c.PIPE_WAIT;
    return c.SetNamedPipeHandleState(pipe, &mode, null, null) != 0;
}

fn closeWindowsForProcess(pid: u32) void {
    const Callback = struct {
        fn call(window: c.HWND, process_id: c.LPARAM) callconv(.c) c.BOOL {
            var owner: c.DWORD = 0;
            _ = c.GetWindowThreadProcessId(window, &owner);
            if (owner == @as(u32, @intCast(process_id))) _ = c.PostMessageW(window, c.WM_CLOSE, 0, 0);
            return 1;
        }
    };
    _ = c.EnumWindows(Callback.call, @intCast(pid));
}

test {
    _ = audio;
    _ = registry;
    _ = backoff;
    _ = media;
    _ = providers;
}
